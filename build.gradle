buildscript {

    ext {

        // --- Framework versions ---

        // Kotlin with Coroutines
        kotlinVersion = '1.4.21'
        coroutinesVersion = '1.4.2'

        // Android with Jetpack Compose
        androidGradlePluginVersion = '7.0.0-alpha10'
        composeVersion = '1.0.0-alpha10'
        composeNavigationVersion = '1.0.0-alpha05'

        // Android Jetpack Libs
        roomVersion = '2.2.6'

        // Third Party
        retrofitVersion = '2.9.0'
    }

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$androidGradlePluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "$detektVersion"
    id "org.sonarqube" version "$sonarqubeVersion"
}

detekt {
    allRules = true
    config = files("$projectDir/detekt.yml")

    // Don't break the build. Just report.
    ignoreFailures = true
}

sonarqube {
    properties {
        property "sonar.projectKey", "StefanOltmann_smart-home-android"
        property "sonar.organization", "stefanoltmann"
        property "sonar.host.url", "https://sonarcloud.io"
        // Include Detekt issues
        property "sonar.kotlin.detekt.reportPaths", "build/reports/detekt/detekt.xml"
    }
}

// SonarQube should always contain Detekt issues
tasks.findByPath(':sonarqube').dependsOn('detekt')

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
