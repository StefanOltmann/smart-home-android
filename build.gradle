buildscript {

    ext {

        // --- Framework versions ---

        detektVersion = '1.17.1'
        sonarqubeVersion = '3.2.0'

        // Kotlin with Coroutines
        kotlinVersion = '1.4.21'
        coroutinesVersion = '1.4.2'

        // Android with Jetpack Compose
        androidGradlePluginVersion = '7.2.0-alpha03'
        composeVersion = '1.0.0-alpha10'
        composeNavigationVersion = '1.0.0-alpha05'

        // Android Jetpack Libs
        roomVersion = '2.3.0'

        // Third Party
        retrofitVersion = '2.9.0'
    }

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$androidGradlePluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "$detektVersion"
    id "org.sonarqube" version "$sonarqubeVersion"
}

sonarqube {
    properties {
        property "sonar.projectKey", "smart-home-android"
        property "sonar.projectName", "Stefans Smart Home Android App"
        property "sonar.organization", "stefanoltmann"
        property "sonar.host.url", "https://sonarcloud.io"
        // Include Detekt issues
        property "sonar.kotlin.detekt.reportPaths", "app/build/reports/detekt/detekt.xml"
        // Include Android Lint
        property "sonar.android.lint.report", "app/build/reports/lint-results.xml"
    }
}

subprojects {

    apply plugin: 'io.gitlab.arturbosch.detekt'

    detekt {
        allRules = true
        config = files("$projectDir/../detekt.yml")

        // Don't break the build. Just report.
        ignoreFailures = true
    }
}

// SonarQube should always contain Detekt issues
tasks.findByPath(':sonarqube').dependsOn(':app:detekt', ':app:lint')

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
